---

title: Difference between QUERYFILTER and QUERYBUILDRANGE
description: Difference between QUERYFILTER and QUERYBUILDRANGE
author: Duy Dang
modified: 2017-01-01
categories: [ax2012, sql, trick]
tags: [buildquery, programming, QUERYFILTER, QUERYBUILDRANGE]
key: d365
---

In Dynamics AX, we have two way to filter the result set of records in joined query is using `QueryFilter` class and `QueryBuildRange` class.

So what's difference between them? when do we use `QueryFilter` class? When do we use `QueryBuildRange` class?

Today, i will make a simple sample to show what is difference between them. You can download example project from _[here](https://1drv.ms/u/s!AuMuU-6qq5hxiYQLElp6RoLgmB_uIA)_.

I have two table :
<!--more-->
Table `DuyDang_ParentTable` which have 1 columns : `ID`.

![Parent_Table]({{site.url}}/assets/imagesposts/Parent_Table.jpg)

Table `DuyDang_ChildTable` which have 3 columns : `ID`, `ParentID`, `Qty`.

![Child_Table]({{site.url}}/assets/imagesposts/Child_Table.jpg)

and they have relation like this : `DuyDang_ParentTable.ID = DuyDang_ChildTable.ParentID`.

I have the **Outer Join** query:

### Use QueryFilter class:

{% highlight csharp %}
static void DuyDang_QueryFilter(Args _args)
{
    Query query;
    QueryBuildDataSource qbds, qbds1;
    QueryRun queryRun;    
    DuyDang_ParentTable parentTable;
    DuyDang_ChildTable childTable;
    QueryFilter qFilter;
    QueryBuildRange qRange;
    struct structSet;

    structSet = new struct
        ("str ParentID;"
        + "str ChildID;"
        + "real Quantity"
        );

    query = new Query();
    qbds = query.addDataSource(tableNum(DuyDang_ParentTable));  
    qbds1 = qbds.addDataSource(tableNum(DuyDang_ChildTable));    
    qbds1.joinMode(JoinMode::OuterJoin); // Set join type.   
    qbds1.addLink(fieldNum(DuyDang_ParentTable, ID), fieldNum(DuyDang_ChildTable, ParentID));
    qFilter = query.addQueryFilter(qbds1, 'Qty');
    qFilter.value(queryValue(15));
    //qRange = qbds1.addRange(fieldNum(DuyDang_ChildTable, Qty));
    //qRange.value(queryValue(15));
    
    queryRun = new QueryRun(query);    
    while (queryRun.next())
    {
        parentTable = queryRun.get(tableNum(DuyDang_ParentTable));
        childTable = queryRun.get(tableNum(DuyDang_ChildTable));

        structSet.value("ParentID", parentTable.ID);
        structSet.value("ChildID", childTable.ID);
        structSet.value("Quantity", childTable.Qty);
        info(structSet.toString());
    }
}
{% endhighlight %}

and result `info` here:

![Child_Table]({{site.url}}/assets/imagesposts/Result_Filter.jpg)

### Use QueryBuildRange class:

{% highlight csharp %}
static void DuyDang_QueryFilter(Args _args)
{
    Query query;
    QueryBuildDataSource qbds, qbds1;
    QueryRun queryRun;    
    DuyDang_ParentTable parentTable;
    DuyDang_ChildTable childTable;
    QueryFilter qFilter;
    QueryBuildRange qRange;
    struct structSet;

    structSet = new struct
        ("str ParentID;"
        + "str ChildID;"
        + "real Quantity"
        );

    query = new Query();
    qbds = query.addDataSource(tableNum(DuyDang_ParentTable));  
    qbds1 = qbds.addDataSource(tableNum(DuyDang_ChildTable));    
    qbds1.joinMode(JoinMode::OuterJoin); // Set join type.   
    qbds1.addLink(fieldNum(DuyDang_ParentTable, ID), fieldNum(DuyDang_ChildTable, ParentID));
    //qFilter = query.addQueryFilter(qbds1, 'Qty');
    //qFilter.value(queryValue(15));
    qRange = qbds1.addRange(fieldNum(DuyDang_ChildTable, Qty));
    qRange.value(queryValue(15));
    
    queryRun = new QueryRun(query);    
    while (queryRun.next())
    {
        parentTable = queryRun.get(tableNum(DuyDang_ParentTable));
        childTable = queryRun.get(tableNum(DuyDang_ChildTable));

        structSet.value("ParentID", parentTable.ID);
        structSet.value("ChildID", childTable.ID);
        structSet.value("Quantity", childTable.Qty);
        info(structSet.toString());
    }
}

{% endhighlight %}

and result `info` here:

![Child_Table]({{site.url}}/assets/imagesposts/Result_Range.jpg)


now you can see the difference between them

- When you use `QueryBuidRange` class, the restriction is in the `ON` clause of the `OUTER JOIN` in the ANSI SQL select statement that is generated by the AOS for the underlying database system.

{% highlight sql %}
SELECT * FROM DuyDang_ParentTable(DuyDang_ParentTable_1)
	OUTER JOIN * FROM DuyDang_ChildTable(DuyDang_ChildTable_1) 
	ON DuyDang_ParentTable.ID = DuyDang_ChildTable.ParentID 
		AND ((Qty = 15))
{% endhighlight %}

- When you use `QueryFilter` class, the restriction is in the `WHERE` clause of the `OUTER JOIN` in the ANSI SQL select statement that is generated by the AOS for the underlying database system.

{% highlight sql %}
SELECT * FROM DuyDang_ParentTable(DuyDang_ParentTable_1) 
	OUTER JOIN * FROM DuyDang_ChildTable(DuyDang_ChildTable_1) 
	ON DuyDang_ParentTable.ID = DuyDang_ChildTable.ParentID 
	WHERE ((DuyDang_ChildTable(DuyDang_ChildTable_1).Qty = 15))
{% endhighlight %}

Thank you for reading!
